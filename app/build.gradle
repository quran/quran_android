apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
if (getGradle().getStartParameter().getTaskRequests().toString().contains("Release") &&
    !project.hasProperty("disableFirebase")) {
  apply plugin: 'com.google.gms.google-services'
}

android {
  compileSdkVersion deps.android.build.compileSdkVersion

  lintOptions {
    checkReleaseBuilds true
    lintConfig file("lint.xml")
    enable "Interoperability"
  }

  defaultConfig {
    minSdkVersion deps.android.build.minSdkVersion
    targetSdkVersion deps.android.build.targetSdkVersion
    versionCode 3070
    versionName "3.0.7"
    testInstrumentationRunner "com.quran.labs.androidquran.core.QuranTestRunner"
    multiDexEnabled true
  }

  signingConfigs {
    release {
      storeFile file(STORE_FILE)
      storePassword STORE_PASSWORD
      keyAlias KEY_ALIAS
      keyPassword KEY_PASSWORD
    }
  }

  flavorDimensions "pageType"
  productFlavors {
    madani {
      applicationId "com.quran.labs.androidquran"
    }
  }

  buildTypes {
    beta {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
      signingConfig signingConfigs.release
      versionNameSuffix "-beta"
      if (project.hasProperty('disableCrashlytics')) {
        firebaseCrashlytics.mappingFileUploadEnabled false
      }
      matchingFallbacks = ['debug']
    }

    debug {
      firebaseCrashlytics.mappingFileUploadEnabled false
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }

    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
      signingConfig signingConfigs.release
      if (project.hasProperty('disableCrashlytics')) {
        firebaseCrashlytics.mappingFileUploadEnabled false
      }
    }
  }

  applicationVariants.all { variant ->
    resValue "string", "authority", applicationId + '.data.QuranDataProvider'
    resValue "string", "file_authority", applicationId + '.fileprovider'
    if (applicationId.endsWith("debug")) {
      mergedFlavor.manifestPlaceholders = [app_debug_label: "Quran " + flavorName.capitalize()]
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }

  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
      outputs.upToDateWhen { false }
      showStandardStreams true
      exceptionFormat "full"
    }
  }

  packagingOptions {
    exclude "META-INF/*.kotlin_module"
  }
}

// required so that Errorprone doesn't look at generated files
afterEvaluate {
  tasks.withType(JavaCompile).configureEach {
    options.errorprone.excludedPaths = ".*/build/generated/.*"
    options.errorprone.disableWarningsInGeneratedCode = true
  }
}

if (new File(rootDir, 'extras/extras.gradle').exists()) {
   apply from: new File(rootDir, 'extras/extras.gradle')
} else {
   apply from: 'pluggable.gradle'
}

dependencies {
  implementation project(path: ':common:analytics')
  implementation project(path: ':common:audio')
  implementation project(path: ':common:data')
  implementation project(path: ':common:networking')
  implementation project(path: ':common:pages')
  implementation project(path: ':common:search')
  implementation project(path: ':common:upgrade')

  implementation project(path: ':feature:audio')

  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutinesVersion}"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutinesVersion}"

  implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  implementation "com.squareup.retrofit2:converter-moshi:${retrofitVersion}"

  implementation "androidx.appcompat:appcompat:${androidxAppcompatVersion}"
  implementation "androidx.media:media:${androidxMediaVersion}"
  implementation "androidx.localbroadcastmanager:localbroadcastmanager:${androidxLocalBroadcastVersion}"
  implementation "androidx.preference:preference-ktx:${androidxPreferencesVersion}"
  implementation "androidx.recyclerview:recyclerview:${androidxRecyclerViewVersion}"
  implementation "com.google.android.material:material:${materialComponentsVersion}"
  implementation "androidx.swiperefreshlayout:swiperefreshlayout:${androidxSwipeRefreshVersion}"
  implementation "androidx.multidex:multidex:2.0.1"

  // rx
  implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

  // dagger
  kapt deps.dagger.apt
  kaptAndroidTest deps.dagger.apt
  implementation deps.dagger.runtime

  // workmanager
  implementation "androidx.work:work-runtime-ktx:${workManagerVersion}"

  implementation "com.squareup.okio:okio:${okioVersion}"
  implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"

  implementation "com.squareup.moshi:moshi:${moshiVersion}"
  kapt("com.squareup.moshi:moshi-kotlin-codegen:${moshiVersion}")

  implementation "dev.chrisbanes:insetter-ktx:0.3.1"
  implementation 'com.jakewharton.timber:timber:4.7.1'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
  implementation 'com.google.firebase:firebase-crashlytics:17.4.0'

  testImplementation "junit:junit:${junitVersion}"
  testImplementation "com.google.truth:truth:${truthVersion}"
  testImplementation "org.mockito:mockito-core:3.8.0"
  testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"

  androidTestImplementation "androidx.test.espresso:espresso-core:${espressoVersion}"
  androidTestImplementation "androidx.test.espresso:espresso-intents:${espressoVersion}"
  androidTestImplementation "androidx.test.ext:junit:${androidxJunitExtVersion}"

  errorprone 'com.google.errorprone:error_prone_core:2.5.1'
  //noinspection GradleDynamicVersion
  errorproneJavac("com.google.errorprone:javac:9+181-r4173-1")

  // Number Picker
  implementation 'com.shawnlin:number-picker:2.4.12'
}
