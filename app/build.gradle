apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    lintOptions {
      checkReleaseBuilds true
      lintConfig file("lint.xml")
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 2720
        versionName "2.7.2"
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    productFlavors {
        madani {
            applicationId "com.quran.labs.androidquran"
            resValue "string", "authority", applicationId + '.data.QuranDataProvider'
            resValue "string", "file_authority", applicationId + '.fileprovider'
        }

        qaloon {
            applicationId "com.quran.labs.androidquran.qaloon"
            resValue "string", "authority", applicationId + '.data.QuranDataProvider'
            resValue "string", "file_authority", applicationId + '.fileprovider'
            versionCode 1050
            versionName "1.0.5"
        }

        naskh {
            applicationId "com.quran.labs.androidquran.naskh"
            resValue "string", "authority", applicationId + '.data.QuranDataProvider'
            resValue "string", "file_authority", applicationId + '.fileprovider'
            versionCode 1050
            versionName "1.0.5"
        }

        shemerly {
            applicationId "com.quran.labs.androidquran.shemerly"
            resValue "string", "authority", applicationId + '.data.QuranDataProvider'
            resValue "string", "file_authority", applicationId + '.fileprovider'
            versionCode 1050
            versionName "1.0.5"
        }
    }

    buildTypes {
      beta {
         minifyEnabled true
         proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
         signingConfig signingConfigs.release
         versionNameSuffix "-beta"
         if (project.hasProperty('disableCrashlytics')) {
            ext.enableCrashlytics = false
         }
      }

      debug {
         ext.enableCrashlytics = false
      }

      release {
         minifyEnabled true
         proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
         signingConfig signingConfigs.release
         if (project.hasProperty('disableCrashlytics')) {
            ext.enableCrashlytics = false
         }
      }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions.unitTests.all {
      testLogging {
         events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
         outputs.upToDateWhen { false }
         showStandardStreams = true
      }
    }
}

ext {
   supportLibVersion = '25.0.1'
}

dependencies {
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'com.google.dagger:dagger:2.0'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'com.squareup.moshi:moshi:1.0.0'
    compile 'com.jakewharton.timber:timber:4.1.0'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.27'
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile 'com.squareup.okhttp3:mockwebserver:3.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
