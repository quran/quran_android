import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.internal.KaptGenerateStubsTask
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  id "quran.android.application"
  alias libs.plugins.kotlin.kapt
  alias libs.plugins.kotlin.parcelize
  alias libs.plugins.ksp
  alias libs.plugins.errorprone
  alias libs.plugins.anvil
}

// whether or not to use Firebase - Firebase is enabled by default, and is only disabled for
// providing apks for open source distribution stores.
def useFirebase = !project.hasProperty("disableFirebase") &&
    !project.hasProperty("disableCrashlytics")

// only want to apply the Firebase plugin if we're building a release build. moving this to the
// release build type won't work, since debug builds would also implicitly get the plugin.
if (getGradle().getStartParameter().getTaskRequests().toString().contains("Release") && useFirebase) {
  apply plugin: 'com.google.gms.google-services'
  apply plugin: 'com.google.firebase.crashlytics'
}

android {
  namespace 'com.quran.labs.androidquran'

  defaultConfig {
    versionCode 3441
    versionName "3.4.4"
    testInstrumentationRunner "com.quran.labs.androidquran.core.QuranTestRunner"
  }

  dependenciesInfo {
    // only keep dependency info block for builds with Firebase
    includeInApk useFirebase
    includeInBundle useFirebase
  }

  buildFeatures.buildConfig = true

  signingConfigs {
    release {
      storeFile file(STORE_FILE)
      storePassword STORE_PASSWORD
      keyAlias KEY_ALIAS
      keyPassword KEY_PASSWORD
    }
  }

  flavorDimensions = ["pageType"]
  productFlavors {
    madani {
      applicationId "com.quran.labs.androidquran"
    }
  }

  buildTypes {
    beta {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
      signingConfig signingConfigs.release
      versionNameSuffix "-beta"
      matchingFallbacks = ['debug']
    }

    debug {
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }

    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
      signingConfig signingConfigs.release
    }
  }

  applicationVariants.configureEach { variant ->
    resValue "string", "authority", applicationId + '.data.QuranDataProvider'
    resValue "string", "file_authority", applicationId + '.fileprovider'
    if (applicationId.endsWith("debug")) {
      mergedFlavor.manifestPlaceholders = [app_debug_label: "Quran " + flavorName.capitalize()]
    }
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
      all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          outputs.upToDateWhen { false }
          showStandardStreams true
          exceptionFormat "full"
        }
      }
    }
  }

  packagingOptions {
    resources {
      excludes += ['META-INF/*.kotlin_module', 'META-INF/DEPENDENCIES', 'META-INF/INDEX.LIST']
    }
  }
}

tasks.withType(KotlinCompile).configureEach {
  // TODO necessary until anvil supports something for K2 contribution merging
  compilerOptions {
    progressiveMode.set(false)
    languageVersion.set(KotlinVersion.KOTLIN_1_9)
  }
}

// required so that Errorprone doesn't look at generated files
afterEvaluate {
  tasks.withType(JavaCompile).configureEach {
    options.errorprone.excludedPaths = ".*/build/generated/.*"
    options.errorprone.disableWarningsInGeneratedCode = true
  }
}

if (new File(rootDir, 'extras/extras.gradle').exists()) {
   apply from: new File(rootDir, 'extras/extras.gradle')
} else {
   apply from: 'pluggable.gradle'
}

dependencies {
  implementation project(path: ':common:analytics')
  implementation project(path: ':common:audio')
  implementation project(path: ':common:bookmark')
  implementation project(path: ':common:data')
  implementation project(path: ':common:di')
  implementation project(path: ':common:download')
  implementation project(path: ':common:networking')
  implementation project(path: ':common:pages')
  implementation project(path: ':common:preference')
  implementation project(path: ':common:reading')
  implementation project(path: ':common:recitation')
  implementation project(path: ':common:search')
  implementation project(path: ':common:toolbar')
  implementation project(path: ':common:translation')
  implementation project(path: ':common:upgrade')
  implementation project(path: ':common:ui:core')

  implementation project(path: ':feature:audio')
  implementation project(path: ':feature:audiobar')
  implementation project(path: ':feature:downloadmanager')
  implementation project(path: ':feature:qarilist')

  implementation libs.kotlinx.coroutines.core
  implementation libs.kotlinx.coroutines.android

  implementation libs.retrofit
  implementation libs.converter.moshi

  implementation libs.androidx.appcompat
  implementation libs.androidx.media
  implementation libs.androidx.localbroadcastmanager
  implementation libs.androidx.preference.ktx
  implementation libs.androidx.recyclerview
  implementation libs.material
  implementation libs.androidx.swiperefreshlayout
  implementation libs.androidx.window

  // compose
  implementation libs.compose.ui

  // okio
  implementation libs.okio

  // rx
  implementation libs.rxjava
  implementation libs.rxandroid

  // dagger
  kapt libs.dagger.compiler
  kaptTest libs.dagger.compiler
  implementation libs.dagger.runtime

  // workmanager
  implementation libs.androidx.work.runtime.ktx

  implementation libs.okhttp

  implementation libs.moshi
  ksp(libs.moshi.codegen)

  implementation libs.insetter
  implementation libs.timber
  debugImplementation libs.leakcanary.android

  testImplementation libs.junit
  testImplementation libs.truth
  testImplementation libs.mockito.core
  testImplementation libs.okhttp.mockserver
  testImplementation libs.junit.ktx
  testImplementation libs.robolectric
  testImplementation libs.espresso.core
  testImplementation libs.espresso.intents
  testImplementation libs.turbine
  testImplementation libs.kotlinx.coroutines.test
  testImplementation project(":pages:data:madani")

  errorprone libs.errorprone.core

  // Number Picker
  implementation libs.number.picker
}
